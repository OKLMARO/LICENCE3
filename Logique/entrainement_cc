$N=5

;; nationalités
$anglais=1
$suedois=2
$danois=3
$norvegien=4
$allemand=5

;; couleurs
$rouge=1
$vert=2
$blanc=3
$bleu=4
$jaune=5

;; animaux
$chien=1
$oiseau=2
$chat=3
$cheval=4
$poisson=5

;; cigarettes
$pall=1
$dunhill=2
$blend=3
$blue=4
$prince=5


;; boisson
$the=1
$cafe=2
$lait=3
$biere=4
$eau=5

;; couleur unique
bigand $j in [1..$N]:
exact(1,pers_mais($j,[1..$N])) and exact(1,pers_mais([1..$N],$j))
end

;; position unique
bigand $j in [1..$N]:
exact(1,mais_pos($j,[1..$N])) and exact(1,mais_pos([1..$N],$j))
end

;; marque cigares unique
bigand $j in [1..$N]:
exact(1,pers_fume($j,[1..$N])) and exact(1,pers_fume([1..$N],$j))
end

;; boisson unique
bigand $j in [1..$N]:
exact(1,pers_boit($j,[1..$N])) and exact(1,pers_boit([1..$N],$j))
end

;; animal unique
bigand $j in [1..$N]:
exact(1,pers_ani($j,[1..$N])) and exact(1,pers_ani([1..$N],$j))
end

;; 1 L’Anglais vit dans une maison rouge
pers_mais($anglais,$rouge)

;; 2  Le Suédois a des chiens comme animaux domestiques
pers_ani($suedois,$chien)

;; 3 Le Danois boit du thé
pers_boit($danois,$the)

;; 4 La maison verte est à gauche de la maison blanche.
;; idée: il existe deux personnes j1 et j2 et une position p telles que 
;; j1 vit dans la maison verte à la position p et j2 habite dans la maison blanche et elle est en position p+1
bigor $pos in [1..$N-1]:
mais_pos($vert,$pos) and mais_pos($blanc,$pos+1)
end

;; 5 Le propriétaire de la maison verte boit du café
;; traduction: "tout propriétaire j d'une maison verte boit du café" 
;; (cela convient car chaque maison n'a qu'un seul propriétaire)
bigand $j in [1..$N]:
pers_mais($j,$vert) => pers_boit($j,$cafe)
end

;; 6 La personne qui fume des Pall Mall a des oiseaux.
bigand $j in [1..$N]:
pers_fume($j,$pall) => pers_ani($j,$oiseau)
end

;; 7 Le propriétaire de la maison jaune fume des Dunhill
bigand $j in [1..$N]:
pers_mais($j,$jaune) => pers_fume($j,$dunhill)
end

;; 8 La personne qui vit dans la maison du centre boit du lait.
bigand $i,$j in [1..$N],[1..$N]:
mais_pos($j,3) and pers_mais($i,$j) => pers_boit($i,$lait)
end

;; 9 Le Norvégien habite la première maison.
mais_pos($norvegien,1)

;; 10  L’homme qui fume les Blend vit à côté de celui qui a des chats.
;; (on utilise la même idée que pour l'indice 4)
bigor $j1,$j2,$pos1,$pos2,$coul1,$coul2 in [1..$N],[1..$N],[1..$N],[1..$N],[1..$N],[1..$N] 
  when ($pos1 == $pos2+1 or $pos1 == $pos2-1):
(per_fume($j1,$blend) and pers_mais($j1,$coul1) and mais_pos($coul1,$pos1) and
 pers_ani($j2,$chat) and pers_mais($j2,$coul2)) and mais_pos($coul2,$pos2) 
end

;; 11 L’homme qui a un cheval est le voisin de celui qui fume des Dunhill.
bigor $j1,$j2,$pos1,$pos2,$coul1,$coul2 in [1..$N],[1..$N],[1..$N],[1..$N],[1..$N],[1..$N] 
  when ($pos1 == $pos2+1 or $pos1 == $pos2-1):
(pers_fume($j1,$dunhill) and pers_mais($j1,$coul1) and mais_pos($coul1,$pos1) and
 pers_ani($j2,$cheval) and pers_mais($j2,$coul2) and mais_pos($coul2,$pos2))
end

;; 12 Le propriétaire qui fume des Blue Master boit de la bière.
bigand $j in [1..$N]:
 pers_fume($j,$blue) => pers_boit($j,$biere)
end

;; 13. L’Allemand fume des Prince.
pers_fume($allemand,$prince)

;; 14. Le Norvégien vit juste à côté de la maison bleue.
bigor $coul,$pos1,$pos2 in [1..$N],[1..$N],[1..$N] when ($pos1 == $pos2-1 or $pos1 == $pos2+1):
(pers_mais($norvegien,$coul) and mais_pos($coul,$pos1) and mais_pos($bleu,$pos2))
end

;; 15. L’homme qui fume des Blend a un voisin qui boit de l’eau.
bigor $j1,$j2,$coul1, $coul2, $pos1,$pos2 in [1..$N],[1..$N],[1..$N],[1..$N],[1..$N],[1..$N]
  when ($pos1 == $pos2-1 or $pos1 == $pos2+1):
(pers_fume($j1,$blend) and pers_mais($j1,$coul1) and mais_pos($coul1,$pos1) and
 pers_boit($j2,$eau) and pers_mais($j2,$coul2) and mais_pos($coul2,$pos2))
end

